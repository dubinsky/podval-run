plugins {
  id 'com.github.ben-manes.versions' version '0.28.0'
  id 'scala'
  id 'java-library'
  id 'maven-publish'
  id 'com.jfrog.bintray' version '1.8.5'
}

repositories {
  jcenter()
}

dependencies {
  api 'org.scala-lang:scala-library:2.12.11'
}

group = 'org.podval.tools'
version = '0.1.3'
description = 'Recognizes how the code is being run.'

final String projectArtifact = 'org.podval.tools.run'
final String gitHubRepository = 'dubinsky/podval-run'
final String gitHubRepositoryUrl = "https://github.com/$gitHubRepository"

jar.archiveBaseName.set(projectArtifact)

task sourceJar(type: Jar) {
  from sourceSets.main.allSource
  archiveClassifier.set('sources')
}

publishing {
  publications {
    bintrayMavenPublication(MavenPublication) {
      groupId project.group
      artifactId projectArtifact
      version project.version

      from components.java

      artifact sourceJar

      pom {
        name = 'podval-run'
        description = project.description
        url = gitHubRepositoryUrl
        scm {
          url = "$gitHubRepositoryUrl"
          connection = "scm:git:git://github.com/${gitHubRepository}.git"
          developerConnection = "scm:git:ssh://github.com/${gitHubRepository}.git"
        }
        licenses {
          license {
            name = 'The Apache Software License, Version 2.0'
            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            distribution = 'repo'
            comments = 'A business-friendly OSS license'
          }
        }
        organization {
          name = 'Podval Group'
          url = 'http://www.podval.org'
        }
        developers {
          developer {
            id = 'dub'
            name = 'Leonid Dubinsky'
            email = 'dub@podval.org'
            url = 'http://dub.podval.org'
            organization = 'Podval Group'
            organizationUrl = 'http://www.podval.org'
            timezone = '-5'
          }
        }
      }
    }
  }
}

bintray {
  user         = 'dubinsky'
  key          = findProperty('bintrayApiKey')
  publications = ['bintrayMavenPublication']
  dryRun       = false
  publish      = true
  override     = true
  pkg {
    repo                   = 'org.podval'
    name                   = 'org.podval.tools.run'
    desc                   = project.description
    websiteUrl             = gitHubRepositoryUrl
    issueTrackerUrl        = "${gitHubRepositoryUrl}/issues"
    vcsUrl                 = "${gitHubRepositoryUrl}.git"
    githubRepo             = gitHubRepository
    licenses               = ['Apache-2.0']
    labels                 = ['Gradle', 'Maven', 'Intellij']
    publicDownloadNumbers  = true
    githubReleaseNotesFile = 'CHANGELOG.md'
    version { name         = project.version }
  }
}
